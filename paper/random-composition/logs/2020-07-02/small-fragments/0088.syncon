// === ../base ===

token Ident = "[[:lower:]][[:alnum:]_']*"
token UIdent = "[[:upper:]][[:alnum:]_']*"
token Integer = "[0-9]+"

type Exp
grouping "(" Exp ")"

type Stm
grouping "{" Stm "}"

type Pat
grouping "(" Pat ")"

type Type
grouping "(" Type ")"

type Decl


syncon decls: Top = decls:Decl+
syncon main: Decl =
  "main" "{" body:Stm "}"

infix right seqComp: Stm = ";"
syncon letStm: Stm = "let" p:Pat (":" t:Type)? "=" e:Exp
syncon return: Stm = "return" e:Exp?

syncon intLit: Exp = i:Integer
syncon var: Exp = i:Ident

syncon wildNamePat: Pat = i:Ident
syncon wildNoNamePat: Pat = "_"
syncon intPat: Pat = i:Integer

syncon namedType: Type = n:UIdent



// === inspired/bitwise ===

prefix bitwiseNeg: Exp = "~"
infix left bitwiseLeftShift: Exp = "<<"
infix left bitwiseRightShiftSign: Exp = ">>"
infix left bitwiseRightShiftZero: Exp = ">>>"
infix left bitwiseAnd: Exp = "&"
infix left bitwiseXOr: Exp = "^"
infix left bitwiseOr: Exp = "|"

precedence {
  bitwiseNeg;
  bitwiseLeftShift bitwiseRightShiftSign bitwiseRightShiftZero;
  bitwiseAnd;
  bitwiseXOr;
  bitwiseOr;
}

